
import ps-multitool:json_files as json

append function_tag load:load {"values":["ps-multitool:load"]}
function ps-multitool:load:
    scoreboard objectives add ps-multitool dummy

    schedule function ps-multitool:tick 1t replace

function ps-multitool:tick:
    schedule function ps-multitool:tick 1t replace
    scoreboard players reset @s ps-multitool
    execute as @a[predicate=ps-multitool:has_multitool]
        at @s
        anchored eyes
        positioned ^ ^ ^
        run function ps-multitool:multitool_tick:
            scoreboard players set @s ps-multitool 1
            execute unless score .ray_range ps-multitool matches 1.. run scoreboard players set .ray_range ps-multitool 16
            scoreboard players remove .ray_range ps-multitool 1
            execute if block ~ ~ ~ #minecraft:mineable/axe
                unless predicate ps-multitool:tool/axe
                run loot replace entity @s weapon.mainhand loot ps-multitool:swap/axe
            execute if block ~ ~ ~ #minecraft:mineable/hoe
                unless predicate ps-multitool:tool/hoe
                run loot replace entity @s weapon.mainhand loot ps-multitool:swap/hoe
            execute if block ~ ~ ~ #minecraft:mineable/pickaxe
                unless predicate ps-multitool:tool/pickaxe
                run loot replace entity @s weapon.mainhand loot ps-multitool:swap/pickaxe
            execute if block ~ ~ ~ #minecraft:mineable/shovel
                unless predicate ps-multitool:should_hoe
                unless predicate ps-multitool:tool/shovel
                run loot replace entity @s weapon.mainhand loot ps-multitool:swap/shovel
            execute if predicate ps-multitool:should_hoe
                unless predicate ps-multitool:tool/hoe
                run loot replace entity @s weapon.mainhand loot ps-multitool:swap/hoe
            execute if score .ray_range ps-multitool matches 1..
                unless block ~ ~ ~ #minecraft:mineable/axe
                unless block ~ ~ ~ #minecraft:mineable/hoe
                unless block ~ ~ ~ #minecraft:mineable/pickaxe
                unless block ~ ~ ~ #minecraft:mineable/shovel
                positioned ^ ^ ^0.3
                run function ps-multitool:multitool_tick

function ps-multitool:keep_dura:
    advancement revoke @s only ps-multitool:keep_dura
    item modify entity @s weapon.mainhand ps-multitool:keep_dura

for material in json.materials:
    function f'ps-multitool:craft/{material}_multitool':
        advancement revoke @s only f'ps-multitool:craft/{material}_multitool'
        recipe take @s f'ps-multitool:{material}_multitool'
        clear @s knowledge_book
        loot give @s loot f'ps-multitool:{material}_multitool'
